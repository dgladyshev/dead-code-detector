buildscript {
    ext {
        springBootVersion = '1.5.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'docker'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

checkstyle {
    toolVersion = "7.6"
    ignoreFailures = false
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

findbugs {
    toolVersion = "3.0.1"
    ignoreFailures = false
}

pmd {
    toolVersion = '5.5.4'
    ignoreFailures = false
    ruleSetFiles = files("${rootDir}/config/pmd/ruleset.xml")
    rulePriority = 3
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jar {
    baseName = 'dead-code-detector'
}

task buildDocker(type: Docker, dependsOn: build) {
  push = true
  applicationName = jar.baseName
  dockerfile = file('Dockerfile')
  doFirst {
    copy {
      from jar
      into stageDir
    }
  }
}

repositories {
    mavenCentral()
}

bootRun {
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-neo4j")
    compile 'io.springfox:springfox-swagger2:+'
    compile 'io.springfox:springfox-swagger-ui:2.1.2'
    compile 'net.logstash.logback:logstash-logback-encoder:4.4'
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
    compile "org.projectlombok:lombok:1.16.2"
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '4.6.1.201703071140-r'
    compile group: 'org.buildobjects', name: 'jproc', version: '2.2.1'
    compile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3' //needs to be above junit dependency
    compile 'org.unitils:unitils-core:3.4.2'
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile group: 'com.tngtech.java', name: 'junit-dataprovider', version: '1.12.0'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    compile files('lib/com.scitools.understand.plugin_1.1.3.jar')
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}
